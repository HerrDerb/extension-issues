plugins {
}


subprojects {
    group = 'com.example'
    version = "$version"

    pluginManager.withPlugin("java-library") {
        apply plugin: 'jacoco'
        apply plugin: 'maven-publish'

        dependencies {
            api platform("io.quarkus.platform:quarkus-bom:3.6.0")
            testImplementation platform("io.quarkus.platform:quarkus-bom:3.6.0")

            testImplementation 'org.mockito:mockito-core'
            testImplementation 'org.junit.jupiter:junit-jupiter-api'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        }

        test {
            systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
            useJUnitPlatform()
        }

        jacocoTestReport {
            reports {
                xml.required = true
                csv.required = false
                html.required = false
            }
        }

        java {
            withSourcesJar()
            sourceCompatibility = JavaVersion.VERSION_21
            targetCompatibility = JavaVersion.VERSION_21
        }

        test.finalizedBy jacocoTestReport


        tasks.withType(JavaCompile).tap {
            configureEach {
                options.encoding = 'UTF-8'
                options.compilerArgs << '-Xlint:all'
                options.compilerArgs << '-Xlint:-exports'
                options.compilerArgs << '-Xlint:-requires-automatic'
                options.compilerArgs << '-Xlint:-processing'
                options.deprecation = true
            }
        }
    }
}
